group 'com.thomsonreuters.lsps'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.thomsonreuters.lsps:tr-lsps-io:1.0-SNAPSHOT'
    compile 'org.apache.commons:commons-csv:1.2'
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'TR LSPS DB Utils Library'
                packaging 'jar'
                description 'TR LSPS DB Utils Library'
                url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-db'

                issueManagement {
                    url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-db/issues'
                    system 'GitHub Issues'
                }

                scm {
                    connection 'scm:git:git://github.com/ThomsonReuters-LSPS/tr-lsps-db.git'
                    developerConnection 'scm:git:git@github.com/ThomsonReuters-LSPS/tr-lsps-db.git'
                    url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-db'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'mirasrael'
                        name 'Alexander Bondarev'
                        email 'alexander.bondarev@thomsonreuters.com'
                        url 'https://github.com/mirasrael'
                    }
                }
            }
        }
    }
}
